
plugins {
    id("java")
    id("java-library")
    id("maven-publish")
    id("org.jetbrains.gradle.plugin.idea-ext") version "1.1.7"
    id("eclipse")
    id("com.gtnewhorizons.retrofuturagradle") version "1.3.35"
}

group = 'noobanidus.mods.lootr'
version = '0.6.2'
archivesBaseName = 'lootr-1.12.2'

// Set the toolchain version to decouple the Java we run Gradle with from the Java used to compile and run the mod
java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
    }
}

minecraft {
    mcVersion = "1.12.2"

    extraRunJvmArguments.add("-Dfml.coreMods.load=noobanidus.mods.lootr.LootrCore")
}


repositories {
    mavenCentral()
    maven {
        url "https://maven.cleanroommc.com"
    }
    maven {
        url "https://cursemaven.com"
    }
    maven { url = uri("https://repo.spongepowered.org/repository/maven-public") }
}

dependencies {
    implementation modUtils.enableMixins("zone.rong:mixinbooter:4.2", "mixins.lootr.refmap.json")
    annotationProcessor("org.ow2.asm:asm-debug-all:5.2")
    annotationProcessor("com.google.guava:guava:24.1.1-jre")
    annotationProcessor("com.google.code.gson:gson:2.8.6")
    annotationProcessor("org.spongepowered:mixin:0.8.3") {transitive = false}

    compileOnly rfg.deobf("curse.maven:reccomplex-223150:2689602")
    compileOnly rfg.deobf("curse.maven:ivtoolkit-224535:2443253")
    compileOnly rfg.deobf("curse.maven:dimdoors-284876:3693025")
}

tasks.deobfuscateMergedJarToSrg.configure {accessTransformerFiles.from("src/main/resources/META-INF/lootr_at.cfg")}

jar {
    manifest {
        attributes([
                "FMLCorePluginContainsFMLMod": true,
                "FMLCorePlugin": 'noobanidus.mods.lootr.LootrCore',
                "ForceLoadAsMod": true,
                "FMLAT": "lootr_at.cfg"
        ])
    }
}


processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    filesMatching("mcmod.info") {
        // replace version and mcversion
        expand "version": project.version, "mcversion": project.minecraft.version
    }
}
